class Sector
types
	public SectorID = seq1 of char;
	
instance variables
	
	-- This sector's id
	public id: SectorID;
	-- The sector this one is inserted into, if appliable
	public parent : [Sector] := nil;
	
	inv ancestryIsConsistent();
	
	
operations

	-- Constructor that specifies a parent sector
	public Sector(sectorID: SectorID, parentSector: Sector) sector:Sector == (
		id := sectorID;
		parent := parentSector;
		return self
	)
	pre sectorID <> ""
		and parentSector.id <> sectorID
	post self <> parentSector;
	
	-- Constructs a parent-less sector
	public Sector(sectorID: SectorID) sector:Sector == (
		id := sectorID;
		return self
	)
	pre sectorID <> "";
	
	public getID: () ==> SectorID
	getID() == return id;
	
	public getParent: () ==> Sector
	getParent() == return parent;
	
	-- Returns all ancestor sectors (parent of parent of parent...)
	public getAncestors: () ==> set of Sector
	getAncestors() == (
		dcl parentSector:[Sector] := self.parent;
		dcl ancestors: set of Sector := {};
		
		while(parentSector <> nil) do (
			ancestors := ancestors union {parentSector};
			parentSector := parentSector.parent
		);
		
		return ancestors
	);
	
	-- Checks if there is no ancestor of this sector that references to it
	private ancestryIsConsistent: () ==> bool
	ancestryIsConsistent() == return self not in set self.getAncestors();
	
end Sector